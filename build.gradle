import groovy.json.JsonBuilder

plugins {
    id 'java'
}

group = 'io.github.kurrycat.mpkmod.module.macros'
version = '1.0'
archivesBaseName = 'mpkmacros'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.0-beta9'

    implementation 'com.github.kurrycat2004:mpkmod-api:1.0'
}

jar {
    var config = file("$temporaryDir/mpkmodule.config.json")
    var jsonBuilder = new JsonBuilder()

    jsonBuilder {
        moduleName "mpkmacros"
        mainClass "io.github.kurrycat.mpkmod.module.macros.MPKMacros"
    }

    config.text = jsonBuilder.toPrettyString()
    from config

    destinationDirectory = file("${buildDir}/libs")
}

tasks.register("buildAndMove") {
    dependsOn build
    doLast {
        if (!project.hasProperty("dir")) {
            println "Argument -Pdir missing, please provide Minecraft mods folder path"
        } else {
            copy {
                from "${buildDir}/libs"
                into file(dir)
            }
        }
    }
}